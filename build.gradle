
import java.util.regex.Matcher;
import java.util.regex.Pattern



// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'com.google.gms:google-services:4.2.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile){
            options.compilerArgs << "-Xlint:deprecation"
        }
    }

}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        google()
        maven {
            url "https://maven.google.com"
        }
        maven {
            url "https://jitpack.io"
        }
    }
}

ext {
    //get current flavor of build
    flavor = getCurrentFlavor()
    if(flavor.isEmpty()){
        flavor = "dev"
    }
    //read the .properties for config
    config = getProps('config/' + flavor + '.properties')
}

def getCurrentFlavor(){
    Gradle gradle = getGradle()
    // match optional modules followed by the task
    def pattern = Pattern.compile("([A-Z][A-Za-z]+)(Release|Debug)")
    def flavor = ""

    gradle.getStartParameter().getTaskNames().any{ name ->
        Matcher matcher = pattern.matcher(name)
        if(matcher.find()){
            flavor = matcher.group(1).toLowerCase()
            return  true
        }
    }
    return flavor
}

def getProps(path){
    Properties props = new Properties()
    props.load(new FileInputStream(file(path)))
    return props
}

task clean(type: Delete) {
    delete rootProject.buildDir
}